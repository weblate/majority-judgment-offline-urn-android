plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.room)
    alias(libs.plugins.ksp)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.detekt)
}

// Self-made hack, this, but product flavors didn't run smoothly.  Anyway: this works.
val isGoogleFlavor = providers
    .environmentVariable("GOOGLE")
    .getOrElse("false") == "true"

android {
    namespace = "com.illiouchine.jm"
    compileSdk = 36

    defaultConfig {
        applicationId = if (isGoogleFlavor) {
            // We have to use another applicationId for Google, as Google says
            // > "com.illiouchine.jm is already in use"
            // This is because a malicious actor published a malware-riddled version of our app
            // on Google Play, pretending to be us.  …  -_-
            // Three months later, the offender had been removed, but we kept this application id.
            "fr.mieuxvoter.urn"
        } else {
            // We'd initially registered this app on F-Droid with this applicationId:
            "com.illiouchine.jm"
        }
        minSdk = 27
        targetSdk = 35
        // You need to bump both of these versions when making a new release.
        versionCode = 24
        versionName = "1.6.2"

        // Ideally we'd have both, but support for multiple runners looks experimental
//        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunner = "io.cucumber.android.runner.CucumberAndroidJUnitRunner"
    }

    buildTypes {
        release {
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
    }
    buildFeatures {
        compose = true
    }

    room {
        schemaDirectory("$projectDir/schemas")
    }

    // We find that there is a DependencyInfoBlock in our APK. It's a Signing block added by AGP
    // and encrypted with the Google public key so it can't be read by anyone else but Google.
    // We need to remove it before we publish to F-Droid, as this opaque block is a security hole.
    // https://gitlab.com/fdroid/fdroiddata/-/merge_requests/19981
    dependenciesInfo {
        // Enables/Disables dependency metadata when building APKs.
        includeInApk = isGoogleFlavor
        // Enables/Disables dependency metadata when building Android App Bundles.
        includeInBundle = isGoogleFlavor
    }

    // The "proper" way of having multiple builds seems to be using product flavors.
    // NOPE: Cannot locate tasks that match ':app:assembleDebugUnitTest' as task 'assembleDebugUnitTest' is ambiguous in project ':app'. Candidates are: 'assembleFdroidDebugUnitTest', 'assembleGoogleDebugUnitTest'.
//    flavorDimensions += "store"
//    productFlavors {
//        create("fdroid") {
//            dimension = "store"
//            // We find that there is a DependencyInfoBlock in our APK. It's a Signing block added by AGP
//            // and encrypted with the Google public key so it can't be read by anyone else but Google.
//            // We need to remove it before we publish to F-Droid, as it's a security hole.
//            // https://gitlab.com/fdroid/fdroiddata/-/merge_requests/19981
//            dependenciesInfo {
//                // Disables dependency metadata when building APKs.
//                includeInApk = false
//                // Disables dependency metadata when building Android App Bundles.
//                includeInBundle = false
//            }
//        }
//        create("google") {
//            dimension = "store"
//        }
//    }
}

dependencies {
    // The Usual Suspects
    implementation(libs.kotlinx.serialization.json)
    implementation(libs.kotlinx.serialization.cbor)
    implementation(libs.kotlinx.collections.immutable)

    // Android & Jetpack Compose
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.activity.compose)
    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.androidx.ui)
    implementation(libs.androidx.ui.graphics)
    implementation(libs.androidx.ui.tooling.preview)
    implementation(libs.androidx.material3)
    //implementation("androidx.core:core-splashscreen:1.0.0") // TBD: do we need this?

    // Android App Navigation
    implementation(libs.navigation3.ui)
    implementation(libs.navigation3.runtime)
    implementation(libs.androidx.lifecycle.viewmodel.navigation3)
    implementation(libs.androidx.material3.adaptive.navigation3)

    // Legacy Material Icons
    // See https://developer.android.com/jetpack/androidx/releases/compose-material3#1.4.0
    implementation(libs.androidx.material.icons)

    // Majority Judgment
    implementation(libs.majority.judgment.library.java)

    // Koin (Dependency Injection)
    implementation(project.dependencies.platform(libs.koin.bom))
    implementation(libs.koin.core)
    implementation(libs.koin.android)
    implementation(libs.koin.compose)

    // Room (Database)
    implementation(libs.androidx.room.runtime)
    implementation(libs.androidx.room.ktx)
    ksp(libs.androidx.room.compiler)

    // Plotting
    implementation(libs.compose.charts)
    implementation(libs.koalaplot.core)

    // Data Formats for I/O ; good but commented out because it adds 20Mio to the release (!)
    //implementation("org.jetbrains.kotlinx:dataframe:1.0.0-Beta4")

    // Qr Code Generation
    implementation(libs.qrcode.kotlin)

    // Faking — Development only
    debugImplementation(libs.kotlin.faker)
    //implementation(libs.kotlin.faker)  // adds ~13Mio to our ~3Mio release, so no

    // Testing — Development only
    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso.core)
    androidTestImplementation(platform(libs.androidx.compose.bom))
    androidTestImplementation(libs.androidx.ui.test.junit4)
    androidTestImplementation(libs.cucumber.android)
    androidTestImplementation(libs.cucumber.picocontainer)
    debugImplementation(libs.androidx.ui.tooling)
    debugImplementation(libs.androidx.ui.test.manifest)

    // Static Analysis — Development only
    detektPlugins(libs.detekt.formatting)
}

// Static code analyzer, run with either:
//     ./gradlew detekt
//     ./gradlew detekt --auto-correct --rerun
//     make lint
detekt {
    // Builds the AST in parallel. Rules are always executed in parallel.
    // Can lead to speedups in larger projects. `false` by default.
    parallel = false

    // Define the detekt configuration(s) we use.
    // Defaults to the default detekt configuration.
    config.setFrom("../detekt.yml")

    // Applies the config files on top of detekt's default config file. `false` by default.
    buildUponDefaultConfig = true

    // Turns on all the rules. `false` by default.
    allRules = false

    // Disables all default detekt rulesets and will only run detekt with custom rules
    // defined in plugins passed in with `detektPlugins` configuration. `false` by default.
    disableDefaultRuleSets = false

    // Adds debug output during task execution. `false` by default.
    debug = false

    // If set to `true` the build does not fail when there are any issues.
    // Defaults to `false`.
    ignoreFailures = true

    // The build fails when there is at least one issue with this severity (or above).
    // If set to `Never`, the task will not fail regardless of the issues and their severities.
    // If `ignoreFailures` is set to `true`, the value of this property is ignored.
    // Defaults to `Error`.
    //failOnSeverity = io.gitlab.arturbosch.detekt.extensions.FailOnSeverity.Error

    // Android: Don't create tasks for the specified build types (e.g. "release")
    ignoredBuildTypes = listOf("release")

    // Android: Don't create tasks for the specified build flavor (e.g. "production")
    ignoredFlavors = listOf("production")

    // Android: Don't create tasks for the specified build variants (e.g. "productionRelease")
    ignoredVariants = listOf("productionRelease")
}
